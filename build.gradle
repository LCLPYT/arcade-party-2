plugins {
    id 'java'
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id 'gradle-build-utils' version '1.7.0'
    id 'kibu-plugin-dev' version '0.13.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

Properties props = buildUtils.loadProperties('publish.properties')

group = project.maven_group
version = buildUtils.gitVersion()

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()

    maven {
        url "https://repo.lclpnet.work/repository/internal"
    }

    maven {
        url "https://repo.lclpnet.work/repository/snapshots"
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    maven {
        url = 'https://maven.nucleoid.xyz/'
    }
}

loom {
    // if you want to develop a plugin for dedicated servers, uncomment the following block
    serverOnlyMinecraftJar()

    runs {
        remove client
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "work.lclpnet.mods.kibu:kibu:${project.kibu_version}"
    modImplementation "work.lclpnet.mods.kibu:kibu-world-api:${project.kibu_world_api_version}"
    modImplementation "work.lclpnet.mods.kibu:kibu-plugins:${project.kibu_plugins_version}"
    modImplementation "work.lclpnet.mods:mplugins:${project.mplugins_version}"
    modImplementation "xyz.nucleoid:fantasy:${project.fantasy_version}"
    modImplementation "maven.modrinth:sqlite-jdbc:3.41.2.1+20230506"
    modImplementation "work.lclpnet.mods:combat-control:${project.combat_control_version}"
    modImplementation "work.lclpnet.mods:notica:${project.notica_version}"

    pluginImplementation "work.lclpnet.mcserver-api:mcserver-api-kibu:${project.mcserver_api_version}"
    pluginImplementation "work.lclpnet.plugins:mg-lobby:${project.lobby_version}"
    pluginLocalRuntime "work.lclpnet.plugins:pal:${project.pal_version}"

    implementation 'org.json:json:20231013'
    bundle(implementation('work.lclpnet:json-config4j:1.0.0'))
    bundle(implementation('org.apache.commons:commons-compress:1.26.1'))

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-core:5.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
    testImplementation 'org.xerial:sqlite-jdbc:3.41.2.2'
}

test {
    useJUnitPlatform()
    systemProperty('fabric.side', 'server')
}

processResources {
    inputs.property "version", project.version

    filesMatching("plugin.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

[jar, sourcesJar, shadowJar].each {
    it.from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

shadowJar {
    mergeServiceFiles()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()

            from components.kibu

            pom {
                name = 'Arcade Party 2'
                description = 'Reworked ArcadeParty as kibu minigame.'
            }
        }
    }

    buildUtils.setupPublishRepository(repositories, props)
}